import java.time.LocalDateTime
import java.time.Month
import java.time.temporal.ChronoUnit

plugins {
	id 'com.android.application'
}

android {
	lintOptions {
		disable 'ExpiredTargetSdkVersion'
	}

	namespace 'me.sanyasho.tf_mod'
	compileSdk 29

	packagingOptions {
		jniLibs {
			useLegacyPackaging true
		}
	}

	defaultConfig {
		applicationId "me.sanyasho"
		applicationIdSuffix "tf_mod"
		minSdk 24
		targetSdk 29
		versionCode getBuildNum()
		versionName "1.0.1.8"
		archivesBaseName "tf_mod"
	}

	sourceSets {
		debug {
			java.srcDirs = [ 'src/main/java', 'src/build-debug/java' ]
		}
		release {
			java.srcDirs = [ 'src/main/java', 'src/build-release/java' ]
		}
	}

	buildTypes {
		debug {
			minifyEnabled false
			shrinkResources false
			debuggable true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
		}
		release {
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	implementation 'androidx.appcompat:appcompat:1.3.1'
}

static def getBuildNum() {
	LocalDateTime now = LocalDateTime.now()
	LocalDateTime releaseDate = LocalDateTime.of(2023, Month.FEBRUARY, 18, 0, 0, 0)
	int qBuildNum = releaseDate.until(now, ChronoUnit.DAYS)
	return qBuildNum-1
}
